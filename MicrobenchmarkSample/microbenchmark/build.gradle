apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
// [START benchmark_apply_plugin]
apply plugin: 'androidx.benchmark'
// [END benchmark_apply_plugin]

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 31

        // Set this argument to capture profiling information, instead of measuring performance.
        // Can be one of:
        //   * None
        //   * StackSampling
        //   * MethodTracing
        // See full descriptions of available options at: d.android.com/benchmark#profiling
        testInstrumentationRunnerArguments["androidx.benchmark.profiling.mode"] = 'StackSampling'
    }

    testBuildType = "release"

    buildTypes {
        release {
            // The androidx.benchmark plugin configures release buildType with proper settings, such as:
            // - disables code coverage
            // - adds CPU clock locking task
            // - signs release buildType with debug signing config
            // - copies benchmark results into build/outputs/connected_android_test_additional_output folder
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
    namespace 'com.example.benchmark'
}

// [START benchmark_dependency]
dependencies {
    androidTestImplementation "androidx.benchmark:benchmark-junit4:$benchmark_version"
    androidTestImplementation project(':benchmarkable')
    // [START_EXCLUDE]
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    androidTestImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'

    // duplicate the dependencies of ui module here, to ensure we have the same versions
    androidTestImplementation 'androidx.appcompat:appcompat:1.4.1'
    androidTestImplementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    androidTestImplementation 'androidx.cardview:cardview:1.0.0'
    androidTestImplementation 'androidx.recyclerview:recyclerview:1.2.1'
    // [END_EXCLUDE]
}
// [END benchmark_dependency]
