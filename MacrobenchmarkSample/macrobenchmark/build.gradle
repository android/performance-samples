import com.android.build.api.dsl.ManagedVirtualDevice

plugins {
    id 'com.android.test'
    id 'kotlin-android'
}

// [START macrobenchmark_setup_android]
android {
    // [START_EXCLUDE]
    compileSdk rootProject.compileSdkVersion
    namespace 'com.example.macrobenchmark'

    defaultConfig {
        minSdk 23 // Macrobenchmark doesn't work with SDK lower than 23
        targetSdk rootProject.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    testOptions {
        managedDevices {
            devices {
                pixel6Api31(ManagedVirtualDevice) {
                    device = "Pixel 6"
                    apiLevel = 31
                    systemImageSource = "aosp"
                }
            }
        }
    }
    // [END_EXCLUDE]
    // Note that your module name may have different name
    targetProjectPath = ":app"
    // Enable the benchmark to run separately from the app process
    experimentalProperties["android.experimental.self-instrumenting"] = true

    buildTypes {
        // declare a build type to match the target app's build type
        benchmark {
            debuggable = true
            signingConfig = debug.signingConfig
            // [START_EXCLUDE silent]
            // Selects release buildType if the benchmark buildType not available in other modules.
            matchingFallbacks = ['release']
            // [END_EXCLUDE]
        }
    }
}
// [END macrobenchmark_setup_android]

// [START macrobenchmark_setup_variant]
androidComponents {
    beforeVariants(selector().all()) {
        // enable only the benchmark buildType, since we only want to measure close to release performance
        enabled = buildType == 'benchmark'
    }
}
// [END macrobenchmark_setup_variant]

// [START macrobenchmark_setup_dependencies]
dependencies {
    implementation "androidx.benchmark:benchmark-macro-junit4:$benchmarkVersion"
    // [START_EXCLUDE]
    implementation "androidx.test.espresso:espresso-core:$espressoCoreVersion"
    implementation "androidx.test.ext:junit:$testExtVersion"
    implementation "androidx.test.uiautomator:uiautomator:$uiAutomatorVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    // [END_EXCLUDE]
}
// [END macrobenchmark_setup_dependencies]

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }
}
